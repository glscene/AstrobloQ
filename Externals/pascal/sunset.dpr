program Sunset(Input, Output);

(* ----------------------------------------------------------------------
 Sunset - Время восхода и захода Солнца и Луны
 ----------------------------------------------------------------------- *)

{$APPTYPE CONSOLE}

uses
  Apc.Math,
  Apc.Sun,
  Apc.Moon,
  Apc.Time;

var
  ABOVE, RISE, SETT: Boolean;
  Day, Month, Year, I, IOBJ, NZ: Integer;
  LAMBDA, ZONE, PHI, SPHI, CPHI: Double;
  TSTART, dDate, Hour, HH, UTRISE, UTSET: Double;
  Y_MINUS, Y_0, Y_PLUS, ZERO1, ZERO2, XE, YE: Double;
  SINH0: array [1 .. 3] of Double;

(* -----------------------------------------------------------------------
 SIN_ALT: sin(altitude)
 IOBJ:  1=moon, 2=sun
 ----------------------------------------------------------------------- *)
function SIN_ALT(IOBJ: integer; MJD0, Hour, LAMBDA, CPHI, SPHI: Double): Double;
var
  MJD, T, Ra, Dec, TAU: Double;
begin
  MJD := MJD0 + Hour / 24.0;
  T := (MJD - 51544.5) / 36525.0;
  if (IOBJ = 1) then
    MiniMoon(T, Ra, Dec)
  else
    MiniSun(T, Ra, Dec);
  TAU := 15.0 * (LMST(MJD, LAMBDA) - Ra);
  SIN_ALT := SPHI * SN(Dec) + CPHI * CS(Dec) * CS(TAU);
end;

(* -----------------------------------------------------------------------
 WHM: запись времени в часах и минутах
 ----------------------------------------------------------------------- *)
procedure WHM(UT: Double);
var
  H, M: integer;
begin
  UT := Trunc(UT * 60.0 + 0.5) / 60.0; (* round to 1 min *)
  H := Trunc(UT);
  M := Trunc(60.0 * (UT - H) + 0.5);
  write(H:5, ':', M:2, ' ');
end;

(* -----------------------------------------------------------------------
 GetInp: читает входные данные
 ----------------------------------------------------------------------- *)
procedure GetInp(var dDate, LAMBDA, PHI, ZONE: Double);
var
  D, M, Y: integer;
begin
  writeln;
  writeln('      Sunset: Время восхода и захода Солнца и Луны ');
  writeln;
  write(' First date (yyyy mm dd)               ... ');
  readln(Y, M, D);
  writeln;
  write(' Observing site:  долгота   (l<0 east) ... ');
  readln(LAMBDA);
  write('                  широта               ... ');
  readln(PHI);
  write('                  local time - UT (h)  ... ');
  readln(ZONE);
  writeln;
  writeln;
  write('    Date');
  writeln('             Moon              Sun             Twilight ');
  writeln;
  write('        ');
  writeln('           rise/set          rise/set        end/beginning');
  writeln;
  ZONE := ZONE / 24.0;
  dDate := MJD(D, M, Y, 0) - ZONE;
end;

(* ---------------------------- Sunset ----------------------------------- *)
begin
  SINH0[1] := SN(+8.0 / 60.0); (* moonrise          at h= +8' *)
  SINH0[2] := SN(-50.0 / 60.0); (* sunrise           at h=-50' *)
  SINH0[3] := SN(-12.0); (* nautical twilight at h=-12 degrees *)

  GetInp(TSTART, LAMBDA, PHI, ZONE);

  SPHI := SN(PHI);
  CPHI := CS(PHI);
  for I := 0 to 9 do (* loop over 10 subsequent days *)
  begin
    dDate := TSTART + I;
    CalDat(dDate + ZONE, Day, Month, Year, HH);
    write(Year:5, '/', Month:2, '/', Day:2, '  '); (* print current date *)
    for IOBJ := 1 to 3 do
    begin
      Hour := 1.0;
      Y_MINUS := SIN_ALT(IOBJ, dDate, Hour - 1.0, LAMBDA, CPHI, SPHI) - SINH0[IOBJ];

      ABOVE := (Y_MINUS > 0.0);
      RISE := False;
      SETT := False;

      (* loop over search intervals from [0h-2h] to [22h-24h] *)
      repeat
        Y_0 := SIN_ALT(IOBJ, dDate, Hour, LAMBDA, CPHI, SPHI) - SINH0[IOBJ];
        Y_PLUS := SIN_ALT(IOBJ, dDate, Hour + 1.0, LAMBDA, CPHI, SPHI) - SINH0[IOBJ];
        (* find parabola through three values Y_MINUS,Y_0,Y_PLUS *)
        Quad(Y_MINUS, Y_0, Y_PLUS, XE, YE, ZERO1, ZERO2, NZ);
        case (NZ) of
          0:
            ;
          1:
            if (Y_MINUS < 0.0) then
            begin
              UTRISE := Hour + ZERO1;
              RISE := True;
            end
            else
            begin
              UTSET := Hour + ZERO1;
              SETT := True;
            end;
          2:
            begin
              if (YE < 0.0) then
              begin
                UTRISE := Hour + ZERO2;
                UTSET := Hour + ZERO1;
              end
              else
              begin
                UTRISE := Hour + ZERO1;
                UTSET := Hour + ZERO2;
              end;
              RISE := True;
              SETT := True;
            end;
        end;

        Y_MINUS := Y_PLUS; (* prepare for next interval *)
        Hour := Hour + 2.0;

      until ((Hour = 25.0) or (RISE and SETT));
      if (RISE or SETT) (* output *)
      then
      begin
        if RISE then
          WHM(UTRISE)
        else
          write('----- ':9);
        if SETT then
          WHM(UTSET)
        else
          write('----- ':9);
      end
      else
      begin
        if ABOVE then
          case IOBJ of
            1, 2:
              write('   always visible ');
            3:
              write('    always bright ');
          end
        else
          case IOBJ of
            1, 2:
              write('  always invisible');
            3:
              write('     always dark  ');
          end;
      end;
    end;
    writeln;
  end; (* end of loop over 10 days *)
  writeln;
  write(' all times in local time ( = UT ');
  if ZONE >= 0 then
    write('+');
  writeln(ZONE * 24.0:5:1, 'h )');
end.
