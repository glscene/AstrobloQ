unit LUX.GPU.OpenGL.Window;

interface

uses
  Winapi.Windows,
  System.Classes,
  FMX.Types,
  FMX.Forms,
  LUX,
  LUX.FMX.Forms,
  LUX.GPU.OpenGL;

type
  TGLViewerForm = class(TLuxCommonCustomForm)
  private
  protected
    _WND: HWND;
    _DC: HDC;
    function GetParentWind: HWND;
    procedure CreateHandle; override;
    procedure DestroyHandle; override;
    procedure DoParentFormChanged; override;
    procedure CreateDC;
    procedure DestroyDC;
  public
    constructor CreateNew(Owner_: TComponent; Dummy_: NativeInt = 0); override;
    destructor Destroy; override;
    property ParentWind: HWND read GetParentWind;
    property WND: HWND read _WND;
    property DC: HDC read _DC;
    procedure BeginGL;
    procedure EndGL;
    procedure SwapBuffers;
  end;

  TOepnGL_FMX = class(TOpenGL)
  private
    _Form: TCommonCustomForm;
  protected
    procedure CreateWindow; override;
    procedure DestroyWindow; override;
  public
  end;

implementation //=========================================

uses
  FMX.Controls,
  FMX.Platform.Win,
  Winapi.OpenGLext;

function TGLViewerForm.GetParentWind: HWND;
begin
  Result := WindowHandleToPlatform(ParentForm.Handle).WND;
end;

procedure TGLViewerForm.CreateHandle;
begin
  inherited;

  _WND := WindowHandleToPlatform(Handle).WND;

  SetWindowLong(_WND, GWL_STYLE, WS_CHILD or WS_CLIPSIBLINGS);

  CreateDC;
end;

procedure TGLViewerForm.DestroyHandle;
begin
  DestroyDC;

  inherited;
end;

procedure TGLViewerForm.DoParentFormChanged;
begin
  inherited;

  if Assigned(Parent) and (Parent is TControl) then
    Visible := (Parent as TControl).ParentedVisible;

  if Assigned(ParentForm) then
    Assert(Winapi.Windows.SetParent(_WND, ParentWind) > 0,
      'Failed! TGLViewerForm.CreateHandle');
end;

// ------------------------------------------------------------------------------

procedure TGLViewerForm.CreateDC;
begin
  _DC := GetDC(_WND);

  Assert(_DC > 0, 'Failed! TGLViewerForm.CreateDC');

  _OpenGL_.ApplyPixelFormat(_DC);
end;

procedure TGLViewerForm.DestroyDC;
begin
  ReleaseDC(_WND, _DC);
end;

constructor TGLViewerForm.CreateNew(Owner_: TComponent; Dummy_: NativeInt = 0);
begin
  inherited;

  BorderStyle := TFmxFormBorderStyle.None;

  Show;
  Hide;
end;

destructor TGLViewerForm.Destroy;
begin

  inherited;
end;

procedure TGLViewerForm.BeginGL;
begin
  _OpenGL_.EndGL;

  Assert(wglMakeCurrent(_DC, _OpenGL_.RC), 'Failed! TGLViewerForm.BeginGL');
end;

procedure TGLViewerForm.EndGL;
begin
  Assert(wglMakeCurrent(_DC, 0), 'Failed! TGLViewerForm.EndGL');

  _OpenGL_.BeginGL;
end;

// ------------------------------------------------------------------------------

procedure TGLViewerForm.SwapBuffers;
begin
  Winapi.Windows.SwapBuffers(_DC);
end;

procedure TOepnGL_FMX.CreateWindow;
begin
  _Form := TCommonCustomForm.CreateNew(nil);

  _WND := WindowHandleToPlatform(_Form.Handle).WND;
end;

procedure TOepnGL_FMX.DestroyWindow;
begin
  _Form.Free;
end;

initialization //============================================================

_OpenGL_ := TOepnGL_FMX.Create;

InitOpenGLext;

finalization

end.
