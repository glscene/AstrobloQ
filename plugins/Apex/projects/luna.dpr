program Luna(Input, Output);

(* -----------------------------------------------------------------------
 Luna: Ёфемериды луны
 -----------------------------------------------------------------------*)

{$APPTYPE CONSOLE}

uses
  Apc.Math,
  Apc.PrecNut,
  Apc.Spheric,
  Apc.Moon,
  Apc.Time;

const
  T_OVERLAP = 3.42E-6; (* 3h  in Julian centuries *)
  T_DEVELOP = 2.737850787E-4; (* 10d in Julian centuries *)

var
  Ra, DE, R, PAR, MODJD, Hour: Double;
  T, DT, T_START, T_END, TA, TB: Double;
  Day, Month, Year, NLINE: integer;
  RA_POLY, DE_POLY, R_POLY: TPolynomCheb;

(* -----------------------------------------------------------------------
 GetEph: задание периода времени эфемерид
 ----------------------------------------------------------------------- *)
procedure GetEph(var T1, DT, T2: Double);
var
  Year, Month, Day: integer;
  Hour: Double;
begin
  writeln;
  writeln('                          Luna: Ёфемериды луны            ');
  writeln;
  writeln(' Ќачало и конец эфемерид: ');
  writeln;
  write('  начальна€ дата (yyyy mm dd hh.hhh)  ');
  Readln(Year, Month, Day, Hour);
  T1 := (MJD(Day, Month, Year, Hour) - 51544.5) / 36525.0;
  write('  конечна€ дата (yyyy mm dd hh.hhh)  ');
  Readln(Year, Month, Day, Hour);
  T2 := (MJD(Day, Month, Year, Hour) - 51544.5) / 36525.0;
  write('  шаг времени (dd hh.hh)            ');
  Readln(Day, Hour);
  DT := (Day + Hour / 24.0) / 36525.0;
end;

(* ----------------------------------------------------------------------- *)
(* WRTLBRP: форматированный вывод *)
(* ----------------------------------------------------------------------- *)
procedure WRTLBRP(L, B, R, P: Double);
var
  H, M: integer;
  S: Double;
begin
  DMS(L, H, M, S);
  write(H:5, M:3, S:5:1);
  DMS(B, H, M, S);
  write(H:5, M:3, S:5:1);
  write(R:10:3);
  DMS(P, H, M, S);
  if (H > 0) then
    M := M + 60;
  writeln(M:6, S:6:2);
end;

(* -------------------------------- Luna ----------------------------------- *)
begin
  GetEph(T_START, DT, T_END); (* read desired dates *)
  writeln;
  write('    Date      ET       Ra           Dec      Distance  ');
  writeln(' ѕараллакс ');
  write('              h      h  m  s      o  ''  "  Earth radii');
  writeln('    ''  "   ');

  T := T_START;
  TB := T_START;
  NLINE := 0;

  while (T <= T_END) do
  begin

    if (T > TB - T_OVERLAP) then (* new expansion of the coordinates *)
    begin
      TA := T - T_OVERLAP;
      TB := T + T_DEVELOP + T_OVERLAP;
      T_Fit_Moon(TA, TB, MAX_TP_DEG, RA_POLY, DE_POLY, R_POLY);
    end;

    (* date *)
    MODJD := T * 36525.0 + 51544.5;
    CalDat(MODJD, Day, Month, Year, Hour);
    write(Year:5, '/', Month:2, '/', Day:2, Hour:5:1);

    (* coordinates *)
    Ra := T_Eval(RA_POLY, T) / 15.0;
    if Ra < 0.0 then
      Ra := Ra + 24.0;
    DE := T_Eval(DE_POLY, T);
    R := T_Eval(R_POLY, T);
    PAR := ASN(1.0 / R);

    (* print coordinates *)
    WRTLBRP(Ra, DE, R, PAR);
    NLINE := NLINE + 1;
    if (NLINE MOD 5) = 0 then
      writeln;

    T := T + DT;
  end;
end.
